elua_deps = [eina, eo, efl, ecore, ecore_file, intl]
elua_pub_deps = [lua]

if get_option('lua-interpreter') == 'lua'
  luaver_min = cc.compute_int('LUA_VERSION_NUM - 500',
    prefix: '#include <lua.h>', dependencies: lua
  )
  elua_deps += dependency('cffi-lua-5.@0@'.format(luaver_min))
endif

elua_src = ['elua.c', 'io.c', 'cache.c']
elua_header_src = ['Elua.h', 'elua_api.h']

elua_lib_deps = []
if not sys_windows
  elua_lib_deps = [m, dl]
endif

elua_lib = library('elua',
    elua_src,
    dependencies: elua_lib_deps + elua_deps + elua_pub_deps,
    include_directories : config_dir + [include_directories(join_paths('..','..'))],
    install: true,
    c_args : [package_c_args, '-DELUA_BUILD'],
    version : meson.project_version()
)

elua = declare_dependency(
  include_directories: [include_directories('.')],
  link_with: elua_lib,
  dependencies: elua_pub_deps,
)

install_headers(elua_header_src,
  install_dir : dir_package_include,
)
